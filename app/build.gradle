buildscript {
    repositories {
        mavenCentral()
        maven { url 'https://commondatastorage.googleapis.com/maven-repository/' }
        maven { url "https://jitpack.io" }
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.0.0'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath "com.android.databinding:dataBinder:1.0-rc4"
    }
}
apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.android.databinding'

repositories {
    mavenCentral()
    maven { url 'https://commondatastorage.googleapis.com/maven-repository/' }
    maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
    flatDir {
        dirs '../libs'
    }
    maven { url "https://jitpack.io" }
}


dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    // Android
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.android.support:appcompat-v7:23.2.1'
    compile 'com.android.support:support-v4:23.2.1'
    compile 'com.android.support:recyclerview-v7:23.2.1'
    compile 'com.android.support:cardview-v7:23.2.0'
    compile 'com.android.support:design:23.2.1'

    // Squere
    apt 'com.google.dagger:dagger-compiler:2.4'
    compile 'com.google.dagger:dagger:2.4'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.squareup.picasso:picasso:2.4.0'
    compile 'com.squareup.retrofit:retrofit:1.9.0'
    compile 'com.squareup.okhttp:okhttp-urlconnection:2.2.0'
    compile 'com.squareup.okhttp:okhttp:2.2.0'
    // IO reactivex
    compile 'com.trello:rxlifecycle:0.3.0'
    compile 'com.jakewharton.rxbinding:rxbinding-recyclerview-v7:0.3.0'

    compile 'com.github.frankiesardo:auto-parcel:0.3.1'
    apt 'com.github.frankiesardo:auto-parcel-processor:0.3.1'

    compile 'com.jakewharton.rxbinding:rxbinding:0.3.0'
    compile ('com.jakewharton.rxbinding:rxbinding-appcompat-v7:0.2.0'){
        exclude group: 'com.android.support', module: 'appcompat-v7'
    }
    compile ('com.jakewharton.rxbinding:rxbinding-support-v4:0.2.0') {
        exclude group: 'com.android.support', module: 'appcompat-v4'
    }

    compile 'com.github.jacek-marchwicki.rx-java-extensions:rx-extensions:1.0.4'
    compile 'com.github.jacek-marchwicki.rx-java-extensions:rx-android-extensions:1.0.4'

    // Google
    compile 'com.google.code.findbugs:jsr305:2.0.1'
    compile 'com.google.guava:guava:18.0'

}
def getDate() {
    def date = new Date()
    def formattedDate = date.format('yyyy/MM/dd')
    return formattedDate
}

project.ext.versionCode = 1
project.ext.versionName = "0.0.1-" + getDate()

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.2"

    defaultConfig {
        applicationId "pl.prabel.githubdemo"
        minSdkVersion 15
        targetSdkVersion 23
        testInstrumentationRunner 'android.support.test.runner.AndroidJUnitRunner'
        versionCode project.ext.versionCode
        versionName project.ext.versionName
        multiDexEnabled = true
        useJack false
    }

    dataBinding {
        enabled = true
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
        release {
            storeFile file("debug.keystore")
            storePassword 'android'
            keyAlias 'androiddebugkey'
            keyPassword 'android'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    dexOptions {
        preDexLibraries = false
        javaMaxHeapSize "4g"
    }

    buildTypes {
        debug {
            debuggable true
            minifyEnabled false
            applicationIdSuffix '.debug'
            versionNameSuffix "-debug"
        }
        release {
            signingConfig signingConfigs.release
            debuggable false
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    productFlavors {
        // Define separate dev and prod product flavors.
        dev {
            // dev utilizes minSDKVersion = 21 to allow the Android gradle plugin
            // to pre-dex each module and produce an APK that can be tested on
            // Android Lollipop without time consuming dex merging processes.
            minSdkVersion 21
        }

        prod {
            // The actual minSdkVersion for the application.
            minSdkVersion 15
        }
    }

    lintOptions {
        abortOnError false
        disable 'InvalidPackage'
        xmlOutput file("${reportsDir}/lint/lint-results.xml")
        htmlOutput file("${reportsDir}/lint/lint-results.html")
    }

    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
        exclude 'LICENSE.txt'
        exclude 'LICENSE'
        exclude 'NOTICE.txt'
        exclude 'NOTICE'
        exclude 'asm-license.txt'
    }
}